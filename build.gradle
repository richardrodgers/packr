/*
 * Build file for Packr library.
 * Copyright 2023, Richard Rodgers
 */

plugins {
    id 'java'
    id 'maven-publish'
}

repositories {
    mavenCentral()
    mavenLocal()
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform()
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

group = 'org.modrepo'
archivesBaseName = 'packr'
version = '1.3'
description = 'Compact Java BagIt library'

ext {
    developerName1 = "Richard Rodgers"
    developerId1 = "http://orcid.org/0000-0003-1412-5595"
}

dependencies {
    implementation 'org.apache.commons:commons-compress:1.23.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.10.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.0'
    testImplementation 'org.junit.vintage:junit-vintage-engine:5.10.0'
}

javadoc {
    options.noQualifiers 'all'
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/richardrodgers/${archivesBaseName}")
            credentials {
                username = project.findProperty("gpr.user") ?: "richardrodgers"
                password = project.findProperty("gpr.key") ?: System.getenv("GH_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
